
project(":mobile-web") {

	dependencies {

		compile project(":mobile-api")
		compile files("${rootDir}/mobile-web/src/main/configs")

		compile("com.oracle:ojdbc7:12.1.0.1")

		compile("org.springframework.boot:spring-boot-starter-web")
		compile("org.springframework.boot:spring-boot-starter-web-services")
		compile("org.springframework.boot:spring-boot-starter-aop")
		compile("org.springframework.boot:spring-boot-starter-jdbc")
		compile("org.springframework.boot:spring-boot-starter-jetty")
		compile("org.springframework.boot:spring-boot-starter-security")

		compile("org.springframework.mobile:spring-mobile-device")
		

		compile("io.jsonwebtoken:jjwt:${version_jjwt}")
		compile("com.zaxxer:HikariCP:${version_hikaricp}")

		
		testCompile("org.springframework.boot:spring-boot-starter-test")
		testCompile("org.springframework.boot:spring-boot-test-autoconfigure")
		testCompile("io.rest-assured:rest-assured:3.0.2")
		testCompile("io.rest-assured:json-schema-validator:3.0.2")
		testCompile("io.rest-assured:spring-mock-mvc:3.0.2")



	}

	configurations {
		integTestCompile.extendsFrom testCompile
		integTestRuntime.extendsFrom testRuntime
	}

	sourceSets {
		integTest {
			java {
				srcDir file('src/integ-test/java')
				compileClasspath += main.output + test.output
				runtimeClasspath += main.output + test.output
			}
			resources.srcDir file('src/integ-test/resources')
		}
	}


	task integTest(type: Test) {
		testClassesDir = sourceSets.integTest.output.classesDir
		classpath = sourceSets.integTest.runtimeClasspath
	}

	check.dependsOn integTest

	def configsDir = file("${rootDir}/mobile-web/src/main/configs").absolutePath
	processResources {
		def devConfigsFile = file("${configsDir}/mobile-dev.yml")
		if (!devConfigsFile.exists()) {
			copy {
				from (configsDir) { include "mobile-dev-SAMPLE.yml" }
				into configsDir
				rename { String fileName -> fileName.replace("mobile-dev-SAMPLE.yml", "mobile-dev.yml") }
			}
		}
	}

	springBoot {
		mainClass = "net.syscon.elite.MobileApiServer"
		executable = true
		buildInfo()
	}

	bootRun {
		jvmArgs("-Dspring.profiles.active=dev", "-Dsyscon.configs.dir=${configsDir}")
		addResources = true 
	}

	compileJava.dependsOn processResources

	task run { doLast() { println "Server ready for requests ..." } }
	run.dependsOn bootRun

}


