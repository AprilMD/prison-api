def sourceGenDir = file("${rootDir}/mobile-api/src/main/generated")

project(":mobile-web") {

	apply plugin: "org.springframework.boot"

	dependencies {

		compile project(":mobile-api")
		compile files("${rootDir}/mobile-web/src/main/configs")

        compile("org.flywaydb:flyway-core")
		compile files("${rootDir}/lib/ojdbc7-12.1.0.1.jar")
		runtime('org.hsqldb:hsqldb:2.3.3')

		compile("org.springframework.boot:spring-boot-starter-web")
		compile("org.springframework.boot:spring-boot-starter-aop")
		compile('org.springframework.boot:spring-boot-starter-actuator')
		compile("org.springframework.boot:spring-boot-starter-jdbc")
		compile("org.springframework.boot:spring-boot-starter-jetty")
		compile("org.springframework.boot:spring-boot-starter-security")
		compile("org.springframework.boot:spring-boot-devtools")
		compile("org.springframework.mobile:spring-mobile-device")

		compile("io.jsonwebtoken:jjwt:${version_jjwt}")
		compile("com.zaxxer:HikariCP:${version_hikaricp}")


		testCompile("org.springframework.boot:spring-boot-starter-test")
		testCompile("org.springframework.boot:spring-boot-test-autoconfigure")
		testCompile("io.rest-assured:rest-assured:3.0.2")
		testCompile("io.rest-assured:json-schema-validator:3.0.2")
		testCompile("io.rest-assured:spring-mock-mvc:3.0.2")
		testCompile("org.springframework.security:spring-security-test:4.2.2.RELEASE")
		testCompile("com.google.code.gson:gson:2.8.0")
		testCompile("org.assertj:assertj-core:3.5.2")
	}

	configurations {
		integTestCompile.extendsFrom testCompile
		integTestRuntime.extendsFrom testRuntime
	}

	sourceSets {
		main {
			java {
				srcDirs = ['src/main/java', sourceGenDir]
			}
			resources {
				srcDirs = ['src/main/configs', 'src/main/resources']
			}
		}

		integTest {
			java {
				srcDir file("${rootDir}/mobile-web/src/integ-test/java")
				compileClasspath += main.output + test.output
				runtimeClasspath += main.output + test.output
			}
			resources.srcDir file("${rootDir}/mobile-web/src/integ-test/resources")
		}
	}


	task integTest(type: Test) {
		testClassesDir = sourceSets.integTest.output.classesDir
		classpath = sourceSets.integTest.runtimeClasspath
	}

	check.dependsOn integTest
	compileJava.dependsOn processResources

	def configsDir = file("${rootDir}/mobile-web/src/main/configs").absolutePath
	processResources {
		def devConfigsFile = file("${configsDir}/mobile-dev.yml")
		if (!devConfigsFile.exists()) {
			copy {
				from(configsDir) { include "mobile-dev-SAMPLE.yml" }
				into configsDir
				rename { String fileName -> fileName.replace("mobile-dev-SAMPLE.yml", "mobile-dev.yml") }
			}
		}
	}


	def applicationMainClass = 'net.syscon.elite.MobileApiServer'
	def dateFormat = new java.text.SimpleDateFormat("yyyy-MM-dd HH:mm:ss")

	bootRepackage {
		mainClass = applicationMainClass
	}

	springBoot {
		mainClass = applicationMainClass
		buildInfo {
			// Generate extra build info.
			additionalProperties = [
					by                   : System.properties['user.name'],
					operatingSystem      : "${System.properties['os.name']} (${System.properties['os.version']})",
					continuousIntegration: System.getenv('CI') ? true : false,
					machine              : InetAddress.localHost.hostName,
					time                 : dateFormat.format(new Date()),
					name                 : 'elite2-api'
			]
		}
		executable = true
	}

	bootRun {
		jvmArgs("-Dspring.profiles.active=dev")
		addResources = true
	}

	task run {
		dependsOn bootRun
		doLast() { println "Server ready for requests ..." }
	}

}


