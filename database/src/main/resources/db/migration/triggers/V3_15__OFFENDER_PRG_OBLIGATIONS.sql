CREATE OR REPLACE TRIGGER "OFFENDER_PRG_OBLIGATIONS_TA"
BEFORE INSERT OR UPDATE OR DELETE ON OFFENDER_PRG_OBLIGATIONS
REFERENCING NEW AS NEW OLD AS OLD FOR EACH ROW
  DECLARE
    l_scn NUMBER;
    l_tid VARCHAR2(32);
  BEGIN
    /*
    ============================================================
       Generated by 2.3  Date Generation 10-NOV-2008
    ============================================================
      MODIFICATION HISTORY
       Person       Date      version      Comments
    -----------  --------- -----------  -------------------------------
        GJC      05/03/2007  2.3          Allow application setting some columns
        GJC      23/10/2006  2.2          Audit DELETE statements
       David Ng  18/04/2006  2.0.1        Audit column trigger
    */
    IF INSERTING
    THEN
      :NEW.create_datetime := NVL(:NEW.create_datetime, systimestamp);
      :NEW.create_user_id := NVL(:NEW.create_user_id, user);
    ELSIF UPDATING
      THEN
        :NEW.modify_datetime := systimestamp;
        :NEW.modify_user_id := user;
    END IF;
    IF NOT DELETING
    THEN
      :NEW.Audit_timestamp := systimestamp;
      :NEW.Audit_User_ID := SYS_CONTEXT('NOMIS', 'AUDIT_USER_ID', 30);
      :NEW.Audit_Module_Name := SYS_CONTEXT('NOMIS', 'AUDIT_MODULE_NAME', 65);
      :NEW.Audit_Client_User_ID := SYS_CONTEXT('NOMIS', 'AUDIT_CLIENT_USER_ID', 64);
      :NEW.Audit_Client_IP_Address := SYS_CONTEXT('NOMIS', 'AUDIT_CLIENT_IP_ADDRESS', 39);
      :NEW.Audit_Client_Workstation_Name := SYS_CONTEXT('NOMIS', 'AUDIT_CLIENT_WORKSTATION_NAME', 64);
      :NEW.Audit_Additional_Info := SYS_CONTEXT('NOMIS', 'AUDIT_ADDITIONAL_INFO', 256);
    ELSE
      l_tid := DBMS_TRANSACTION.local_transaction_id(create_transaction=>FALSE);
      SELECT current_scn
      INTO l_scn
      FROM v$database;
      INSERT INTO OMS_DELETED_ROWS
      (
        table_name,
        xid,
        scn,
        audit_timestamp,
        audit_user_id,
        audit_module_name,
        audit_client_user_id,
        audit_client_ip_address,
        audit_client_workstation_name,
        audit_additional_info
      )
      VALUES
        (
          'OFFENDER_PRG_OBLIGATIONS',
          converttoxid(l_tid),
          l_scn,
          systimestamp,
          SYS_CONTEXT('NOMIS', 'AUDIT_USER_ID', 30),
          SYS_CONTEXT('NOMIS', 'AUDIT_MODULE_NAME', 65),
          SYS_CONTEXT('NOMIS', 'AUDIT_CLIENT_USER_ID', 64),
          SYS_CONTEXT('NOMIS', 'AUDIT_CLIENT_IP_ADDRESS', 39),
          SYS_CONTEXT('NOMIS', 'AUDIT_CLIENT_WORKSTATION_NAME', 64),
          SYS_CONTEXT('NOMIS', 'AUDIT_ADDITIONAL_INFO', 256)
        );
    END IF;
  END;

/



CREATE OR REPLACE TRIGGER "OFFENDER_PRG_OBLIGATIONS_TWF"
AFTER UPDATE
  ON OFFENDER_PRG_OBLIGATIONS
REFERENCING NEW AS NEW OLD AS OLD
FOR EACH ROW
  DECLARE
    lv_xml XMLTYPE;
  BEGIN
    -- Lines added by GJC 21/12/2007 to check if the trigger code should be executed or not
    IF SYS_CONTEXT('NOMIS', 'AUDIT_MODULE_NAME', 50) = 'MERGE'
    THEN
      RETURN;
    END IF;
    -- Change added by GJC 21/12/2007 to check if the trigger code should be executed or not
    /*===================================================================
    Version Number = 2.2  Date Modified = 21-DEC-2007
    ====================================================================*/
    /* MODIFICATION HISTORY
       Person      Date           Version       Comments
       ------------------------------------------------------------------
       Graham      21/12/2007     2.2           #7775: Code added for Merge, fix versioning
       NDB         16/06/2007     2.1           Minor Fix
       NDB         16/06/2007     2.0           Created.
    */
    IF :NEW.status != :OLD.status AND :NEW.status IN ('ABA', 'COMP', 'REM')
    THEN
      lv_xml := Tag_Wfmsg.create_xml;
      Tag_Wfmsg.append('offender_sent_condition_id', :NEW.offender_sent_condition_id, lv_xml);
      Tag_workflow.complete_workflow(
          p_trigger_name => 'WORKMESSAGE',
          p_context      => 'PRGASGN_COMP',
          p_params       => lv_xml);
    END IF;
    EXCEPTION
    WHEN OTHERS
    THEN
    Tag_Error.handle();
  END;

/



CREATE OR REPLACE TRIGGER "OFFENDER_PRG_OBLIGATIONS_T2"
AFTER
INSERT
  ON OFFENDER_PRG_OBLIGATIONS
REFERENCING NEW AS NEW OLD AS OLD
FOR EACH ROW
  /* =========================================================
     Version Number = 2.1  Date Modified = 21-DEC-2007
     ========================================================= */

  /* MODIFICATION HISTORY
     Person      Date            Version       Comments
     ---------   ------       ------------  ------------------------------
     Graham      21/12/2007    2.1          #7775: Code added for Merge, fix versioning
     NDB         24/07/2006    2.0          NOMIS project(10.2.0)
  */

  DECLARE

    lv_pgm_rec PROGRAM_SERVICES%ROWTYPE;

  BEGIN
    -- Lines added by GJC 21/12/2007 to check if the trigger code should be executed or not
    IF SYS_CONTEXT('NOMIS', 'AUDIT_MODULE_NAME', 50) = 'MERGE'
    THEN
      RETURN;
    END IF;
    -- Change added by GJC 21/12/2007 to check if the trigger code should be executed or not
    IF (:NEW.program_id IS NOT NULL)
    THEN
      lv_pgm_rec := Tag_Service.get_prg_srv_details(:NEW.program_id);
      IF lv_pgm_rec.program_category = 'ACP'
      THEN
        Tag_Programmes.create_program_profile(
            :NEW.program_id,
            :NEW.offender_prg_obligation_id,
            :NEW.offender_book_id);
      END IF;
    END IF;
    EXCEPTION
    WHEN OTHERS THEN
    tag_error.handle();
  END Offender_Prg_Obligations_T2;


/



CREATE OR REPLACE TRIGGER "OFFENDER_PRG_OBLIGATIONS_T1"
AFTER
INSERT OR UPDATE
  ON Offender_Prg_Obligations
REFERENCING NEW AS NEW OLD AS OLD
FOR EACH ROW
  /* =========================================================
     Version Number = 2.1  Date Modified = 21-DEC-2007
     ========================================================= */

  /* MODIFICATION HISTORY
     Person      Date            Version       Comments
     ---------   ------       ------------  ------------------------------
     Graham      21/12/2007     2.1           #7775: Code added for Merge, fix versioning
     David Ng    03/06/2006     2.0           NOMIS project(10.2.0)
  */
  BEGIN
    -- Lines added by GJC 21/12/2007 to check if the trigger code should be executed or not
    IF SYS_CONTEXT('NOMIS', 'AUDIT_MODULE_NAME', 50) = 'MERGE'
    THEN
      RETURN;
    END IF;
    -- Change added by GJC 21/12/2007 to check if the trigger code should be executed or not
    IF (:old.Status <> :New.Status)
    THEN
      INSERT INTO Offender_Prg_Obligation_HTY
      (OFFENDER_PRG_OBLIGATION_HTY_ID, OFFENDER_PRG_OBLIGATION_ID,
       STATUS, STATUS_CHANGE_DATE, STATUS_CHANGE_REASON)
      VALUES (OFFENDER_PRG_OBLIGATION_HTY_ID.Nextval, :New.OFFENDER_PRG_OBLIGATION_ID,
              :New.STATUS, :new.STATUS_CHANGE_DATE, :new.STATUS_CHANGE_REASON);
    END IF;
  END Offender_Prg_Obligations_T1;

/


