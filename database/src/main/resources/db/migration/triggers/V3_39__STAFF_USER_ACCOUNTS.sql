CREATE OR REPLACE TRIGGER "STAFF_USER_ACCOUNTS_TA"
BEFORE INSERT OR UPDATE OR DELETE ON STAFF_USER_ACCOUNTS
REFERENCING NEW AS NEW OLD AS OLD FOR EACH ROW
  DECLARE
    l_scn NUMBER;
    l_tid VARCHAR2(32);
  BEGIN
    /*
    ============================================================
       Generated by 2.3  Date Generation 10-NOV-2008
    ============================================================
      MODIFICATION HISTORY
       Person       Date      version      Comments
    -----------  --------- -----------  -------------------------------
        GJC      05/03/2007  2.3          Allow application setting some columns
        GJC      23/10/2006  2.2          Audit DELETE statements
       David Ng  18/04/2006  2.0.1        Audit column trigger
    */
    IF INSERTING
    THEN
      :NEW.create_datetime := NVL(:NEW.create_datetime, systimestamp);
      :NEW.create_user_id := NVL(:NEW.create_user_id, user);
    ELSIF UPDATING
      THEN
        :NEW.modify_datetime := systimestamp;
        :NEW.modify_user_id := user;
    END IF;
    IF NOT DELETING
    THEN
      :NEW.Audit_timestamp := systimestamp;
      :NEW.Audit_User_ID := SYS_CONTEXT('NOMIS', 'AUDIT_USER_ID', 30);
      :NEW.Audit_Module_Name := SYS_CONTEXT('NOMIS', 'AUDIT_MODULE_NAME', 65);
      :NEW.Audit_Client_User_ID := SYS_CONTEXT('NOMIS', 'AUDIT_CLIENT_USER_ID', 64);
      :NEW.Audit_Client_IP_Address := SYS_CONTEXT('NOMIS', 'AUDIT_CLIENT_IP_ADDRESS', 39);
      :NEW.Audit_Client_Workstation_Name := SYS_CONTEXT('NOMIS', 'AUDIT_CLIENT_WORKSTATION_NAME', 64);
      :NEW.Audit_Additional_Info := SYS_CONTEXT('NOMIS', 'AUDIT_ADDITIONAL_INFO', 256);
    ELSE
      l_tid := DBMS_TRANSACTION.local_transaction_id(create_transaction=>FALSE);
      SELECT current_scn
      INTO l_scn
      FROM v$database;
      INSERT INTO OMS_DELETED_ROWS
      (
        table_name,
        xid,
        scn,
        audit_timestamp,
        audit_user_id,
        audit_module_name,
        audit_client_user_id,
        audit_client_ip_address,
        audit_client_workstation_name,
        audit_additional_info
      )
      VALUES
        (
          'STAFF_USER_ACCOUNTS',
          converttoxid(l_tid),
          l_scn,
          systimestamp,
          SYS_CONTEXT('NOMIS', 'AUDIT_USER_ID', 30),
          SYS_CONTEXT('NOMIS', 'AUDIT_MODULE_NAME', 65),
          SYS_CONTEXT('NOMIS', 'AUDIT_CLIENT_USER_ID', 64),
          SYS_CONTEXT('NOMIS', 'AUDIT_CLIENT_IP_ADDRESS', 39),
          SYS_CONTEXT('NOMIS', 'AUDIT_CLIENT_WORKSTATION_NAME', 64),
          SYS_CONTEXT('NOMIS', 'AUDIT_ADDITIONAL_INFO', 256)
        );
    END IF;
  END;

/

CREATE OR REPLACE PACKAGE NOMIS_CONTEXT
AS
  -- namespace
  c_namespace   CONSTANT VARCHAR2 (5) := 'NOMIS';

  FUNCTION get_caseload_id
    RETURN VARCHAR2;

  PROCEDURE set_caseload_id ( p_caseload_id IN caseloads.caseload_id%TYPE );

  FUNCTION Show_Version RETURN VARCHAR2;

  -- procedure call upon application events
  PROCEDURE set_context (v_name IN VARCHAR2, v_value IN VARCHAR2);

  -- on after logon set default context
  PROCEDURE set_nomis_context;

  -- on logoff clean up
  PROCEDURE close_session;

  PROCEDURE set_client_nomis_context (p_client_user_id VARCHAR2,
                                      p_client_ip_address VARCHAR2,
                                      p_client_workstation_name VARCHAR2,
                                      p_audit_additional_info VARCHAR2);

END;

/
CREATE OR REPLACE PACKAGE BODY NOMIS_CONTEXT
AS
  -- ======================================================================================
  v_version CONSTANT VARCHAR2(20) := '3.1   08-MAR-2010';
  -- ======================================================================================
  /*
    MODIFICATION HISTORY
     --------------------------------------------------------------------------------------
     Person      Date         Version   Comments
     ---------   -----------  --------- ---------------------------------------------------
     VP         08/03/2010    3.1       Added set_client_nomis_context procedure
     GJC        09/05/2007    1.6       access STAFF_USER_ACCOUNTS only
     GJC        01/05/2007    1.5       Add additional context
     GJC        14/10/2006    1.4       Removed Calls to DBMS_OUTPUT
     David Ng   12/04/2006    2.1       Set up user context for auditing
     A Kapustin 20/04/2006    2.2       Added set_context procedure
     Surya      23/05/2006    2.3       As per the standards, added the show version procedure,
                                        formatted the code and added the version history.
  */
  FUNCTION Show_Version
    RETURN VARCHAR2
  IS
    BEGIN
      RETURN(v_version);
    END Show_Version;

  PROCEDURE set_caseload_id ( p_caseload_id IN caseloads.caseload_id%TYPE )
  IS
    BEGIN
      set_context ('CASELOAD_ID',p_caseload_id);
    END;

  FUNCTION set_caseload_id ( p_user IN varchar2 )
    RETURN VARCHAR2
  IS
    l_caseload_id caseloads.caseload_id%type;
    BEGIN
      SELECT a.working_caseload_id
      INTO   l_caseload_id
      FROM   staff_user_accounts a
      WHERE  a.username = p_user;

      RETURN(l_caseload_id);

      EXCEPTION
      WHEN OTHERS
      THEN
      RETURN(NULL);
    END;

  FUNCTION get_caseload_id
    RETURN VARCHAR2
  IS
    BEGIN

      RETURN(sys_context (c_namespace,'CASELOAD_ID') );

      EXCEPTION
      WHEN OTHERS
      THEN
      RETURN(NULL);
    END;

  PROCEDURE set_session_id
  IS
    BEGIN
      DBMS_SESSION.set_identifier (DBMS_SESSION.unique_session_id);
    END set_session_id;

  PROCEDURE set_context (v_name IN VARCHAR2, v_value IN VARCHAR2)
  AS
    BEGIN
      DBMS_SESSION.set_context (c_namespace,
                                v_name,
                                v_value,
                                USER,
                                DBMS_SESSION.unique_session_id
      );
    END set_context;

  PROCEDURE set_nomis_context
  IS
    BEGIN
      set_session_id;
      set_context ('CASELOAD_ID',set_caseload_id(USER));
      set_context ('AUDIT_USER_ID', USER);
      set_context ('AUDIT_MODULE_NAME', SYS_CONTEXT ('userenv', 'module'));
      set_context ('AUDIT_CLIENT_USER_ID',
                   SYS_CONTEXT ('userenv', 'os_user'));
      set_context ('AUDIT_CLIENT_IP_ADDRESS',
                   SYS_CONTEXT ('userenv', 'ip_address')
      );
      set_context ('AUDIT_CLIENT_WORKSTATION_NAME',
                   SYS_CONTEXT ('userenv', 'terminal')
      );
      set_context ('AUDIT_ADDITIONAL_INFO', NULL);
    END set_nomis_context;

  PROCEDURE close_session
  IS
    BEGIN
      DBMS_SESSION.set_identifier (DBMS_SESSION.unique_session_id);
      DBMS_SESSION.clear_identifier;
    END close_session;

  PROCEDURE close_namespace
  IS
    BEGIN
      -- global cleanup
      DBMS_SESSION.clear_all_context (c_namespace);
    END close_namespace;

  PROCEDURE set_client_nomis_context (p_client_user_id VARCHAR2,
                                      p_client_ip_address VARCHAR2,
                                      p_client_workstation_name VARCHAR2,
                                      p_audit_additional_info VARCHAR2)
  IS
    BEGIN
      -- @@@ Venu 08/03/2010, #17075, Added this procedure to reduce number of database I/O.
      set_context ('AUDIT_CLIENT_USER_ID', p_client_user_id);
      set_context ('AUDIT_CLIENT_IP_ADDRESS', p_client_ip_address);
      set_context ('AUDIT_CLIENT_WORKSTATION_NAME', p_client_workstation_name);
      set_context ('AUDIT_ADDITIONAL_INFO', p_audit_additional_info);
    END set_client_nomis_context;
END nomis_context;
/


CREATE OR REPLACE TRIGGER "STAFF_USER_ACCOUNTS_T1"
BEFORE UPDATE OF working_caseload_id
  ON staff_user_accounts
REFERENCING NEW AS NEW OLD AS OLD
FOR EACH ROW
  BEGIN
    -- Lines added by GJC 21/12/2007 to check if the trigger code should be executed or not
    IF SYS_CONTEXT('NOMIS', 'AUDIT_MODULE_NAME', 50) = 'MERGE'
    THEN
      RETURN;
    END IF;
    -- Change added by GJC 21/12/2007 to check if the trigger code should be executed or not
    /*
       ============================================================
       Version Number = 2.1  Date Modified = 21-DEC-2007
       ============================================================
       MODIFICATION HISTORY
       Person       Date        version      Comments
       ---------    ----------  ---------    ---------------------------------------
       Graham       21/12/2007  2.1          #7775: Code added for Merge, fix versioning
       Surya/GJC    22/07/2007  2.0          Initial Draft.
    */
    IF NVL(:OLD.working_caseload_id, 'X') <> NVL(:NEW.working_caseload_id, 'X')
       AND :NEW.username = USER
    THEN
      nomis_context.set_caseload_id(:NEW.working_caseload_id);
    END IF;
    EXCEPTION
    WHEN OTHERS
    THEN
    tag_error.handle();
  END;

/


