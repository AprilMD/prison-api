CREATE OR REPLACE TRIGGER "OFFENDER_OIC_SANCTIONS_TA"
BEFORE INSERT OR UPDATE OR DELETE ON OFFENDER_OIC_SANCTIONS
REFERENCING NEW AS NEW OLD AS OLD FOR EACH ROW
  DECLARE
    l_scn NUMBER;
    l_tid VARCHAR2(32);
  BEGIN
    /*
    ============================================================
       Generated by 2.3  Date Generation 10-NOV-2008
    ============================================================
      MODIFICATION HISTORY
       Person       Date      version      Comments
    -----------  --------- -----------  -------------------------------
        GJC      05/03/2007  2.3          Allow application setting some columns
        GJC      23/10/2006  2.2          Audit DELETE statements
       David Ng  18/04/2006  2.0.1        Audit column trigger
    */
    IF INSERTING THEN
      :NEW.create_datetime := NVL(:NEW.create_datetime,systimestamp);
      :NEW.create_user_id  := NVL(:NEW.create_user_id,user);
    ELSIF UPDATING THEN
      :NEW.modify_datetime := systimestamp;
      :NEW.modify_user_id  := user;
    END IF ;
    IF NOT DELETING
    THEN
      :NEW.Audit_timestamp               := systimestamp ;
      :NEW.Audit_User_ID                 := SYS_CONTEXT('NOMIS', 'AUDIT_USER_ID', 30) ;
      :NEW.Audit_Module_Name             := SYS_CONTEXT('NOMIS', 'AUDIT_MODULE_NAME', 65) ;
      :NEW.Audit_Client_User_ID          := SYS_CONTEXT('NOMIS', 'AUDIT_CLIENT_USER_ID', 64) ;
      :NEW.Audit_Client_IP_Address       := SYS_CONTEXT('NOMIS', 'AUDIT_CLIENT_IP_ADDRESS', 39) ;
      :NEW.Audit_Client_Workstation_Name := SYS_CONTEXT('NOMIS', 'AUDIT_CLIENT_WORKSTATION_NAME', 64) ;
      :NEW.Audit_Additional_Info         := SYS_CONTEXT('NOMIS', 'AUDIT_ADDITIONAL_INFO', 256) ;
    ELSE
      l_tid := DBMS_TRANSACTION.local_transaction_id(create_transaction=>FALSE);
      SELECT current_scn
      INTO l_scn
      FROM   v$database;
      INSERT INTO OMS_DELETED_ROWS
      (
        table_name,
        xid,
        scn,
        audit_timestamp,
        audit_user_id,
        audit_module_name,
        audit_client_user_id,
        audit_client_ip_address,
        audit_client_workstation_name,
        audit_additional_info
      )
      VALUES
        (
          'OFFENDER_OIC_SANCTIONS',
          converttoxid(l_tid),
          l_scn,
          systimestamp,
          SYS_CONTEXT('NOMIS', 'AUDIT_USER_ID', 30),
          SYS_CONTEXT('NOMIS', 'AUDIT_MODULE_NAME', 65),
          SYS_CONTEXT('NOMIS', 'AUDIT_CLIENT_USER_ID', 64),
          SYS_CONTEXT('NOMIS', 'AUDIT_CLIENT_IP_ADDRESS', 39),
          SYS_CONTEXT('NOMIS', 'AUDIT_CLIENT_WORKSTATION_NAME', 64),
          SYS_CONTEXT('NOMIS', 'AUDIT_ADDITIONAL_INFO', 256)
        );
    END IF;
  END ;

/
ALTER TRIGGER "OFFENDER_OIC_SANCTIONS_TA" ENABLE;


CREATE OR REPLACE TRIGGER "OFFENDER_OIC_SANCTIONS_T1"
BEFORE INSERT
  ON offender_oic_sanctions
REFERENCING NEW AS NEW OLD AS OLD
FOR EACH ROW
  BEGIN
    -- Lines added by GJC 21/12/2007 to check if the trigger code should be executed or not
    IF SYS_CONTEXT('NOMIS','AUDIT_MODULE_NAME',50) = 'MERGE'
    THEN
      RETURN;
    END IF;
    -- Change added by GJC 21/12/2007 to check if the trigger code should be executed or not
    /*
     ============================================================
      Version Number = 2.3  Date Modified = 09/05/2008
    ============================================================
    MODIFICATION HISTORY
    Person       Date        version      Comments
    -----------  ---------   -----------  -------------------------------
    Laurence     09/05/2008  2.3          Removed context check when populating
                                          lids_sanction_number.  Now only does
                                          this when value is null (and is hence
                                          not being passed in from migration).
    Graham       21/12/2007  2.2          #7775: Code added for Merge, fix versioning
    Surya        31/05/2006  2.1          The Lids number should be populated
                                          only from Application, Interface but not
                                       xTag Data migration.
    Surya        30/05/2006  2.0          Initial Draft - Lids sanction
                                          number increments with in current
                                       booking and adjudication.
    */
    if :new.lids_sanction_number is null
    then
      SELECT NVL (MAX (lids_sanction_number), 0) + 1
      INTO :NEW.lids_sanction_number
      FROM offender_oic_sanctions
      WHERE offender_book_id = :NEW.offender_book_id
            AND oic_incident_id = :NEW.oic_incident_id;
    END IF;
    EXCEPTION
    WHEN OTHERS
    THEN
    tag_error.handle;
  END offender_oic_sanctions_t1;

/
ALTER TRIGGER "OFFENDER_OIC_SANCTIONS_T1" ENABLE;


CREATE OR REPLACE TRIGGER "OFF_OIC_SANCTION_API_EVENT"

after insert
  on offender_oic_sanctions
referencing new as new old as old
for each row
  declare
    /******************************************************************************
       Name:  off_oic_sanction_api_event
       PURPOSE: This trigger is part of the api_events interface

       Revisions:
       Ver        Date          Author           Description
       ---------  ------------  ---------------  ------------------------------------
       1.0        29/07/2016    Paul Morris      Initial version
    ******************************************************************************/

      cannot_find_program EXCEPTION;
    PRAGMA EXCEPTION_INIT(cannot_find_program , -06508);

    v_offender_book_id offender_bookings.offender_book_id%type;
    v_agy_loc_id offender_bookings.agy_loc_id%type;
  begin
    -- This trigger is required to fire during merge

    if :new.oic_sanction_code in ('CANTEEN')
       and :new.effective_date <= trunc(sysdate)
       and (add_months(:new.effective_date,:new.sanction_months) + :new.sanction_days) >= trunc(sysdate)
    then

      if api_owner.core_utils.is_in_digital_prison(p_offender_book_id => v_offender_book_id) then
        --
        -- Send transaction message to unilink
        --
        api_owner.pss_events.offender_update(p_offender_book_id => v_offender_book_id,
                                             p_canteen_sanction => true);
      end if;
    end if;

    exception
    when cannot_find_program  then
    -- this needs to propagate so it rectifies itself
    raise;
    when others then
    api_owner.nomis_api_log.error(p_msg_module => 'OFF_OIC_SANCTION_API_EVENT',
                                  p_message    => sqlerrm);
  end;
/
ALTER TRIGGER "OFF_OIC_SANCTION_API_EVENT" ENABLE;


CREATE OR REPLACE TRIGGER "OFF_OIC_SANC_XTAG_EVENTS"
AFTER INSERT OR UPDATE
  ON OFFENDER_OIC_SANCTIONS
REFERENCING NEW AS NEW OLD AS OLD
FOR EACH ROW
WHEN (
  NEW.result_seq IS NOT NULL
) DECLARE
  /******************************************************************************
     NAME:    Off_Oic_Sanc_Xtag_Events
     PURPOSE: This trigger is part of the event monitoring system, please refrain
              from modifying it on your own.

     REVISIONS:
     Ver        Date        Author           Description
     ---------  ----------  ---------------  ------------------------------------
     2.5		  21/07/2008  Laurence         QC#9756. Trigger not to fire during merge.
    2.4        19/07/2007  Igor             Separate HMPS checking for ON INSERT and ON UPDATE
     2.3        18/07/2007  Igor             Only fires for HMPS bookings
     2.2        21-JUL-2006 Laurence         Now passes nomis timestamp to notification.
     2.1        05-JUL-2006 Vipul            modified the trigger to include the status
                                             as part of update clause. TD#3071
     1.0        11/06/2006  Laurence         Created this trigger.
  ******************************************************************************/
BEGIN
  -- Trigger not to fire during merge.
  IF SYS_CONTEXT('NOMIS','AUDIT_MODULE_NAME',50) = 'MERGE'
  THEN
    RETURN;
  END IF;

  IF INSERTING
  THEN
    IF Xtag_Trigger_Utils.is_hmps_booking(:NEW.offender_book_id)
    THEN
      --
      -- IIS A3 event
      --
      xtag.Xtag_Notify
      (v_event_type      => 'A3_RESULT',
       v_params          => xtag.xtag_notification_params
       (xtag.xtag_params ('p_offender_book_id',
                          :NEW.offender_book_id
        ),
        xtag.xtag_params ('p_sanction_seq',
                          :NEW.sanction_seq
        ),
        xtag.xtag_params ('p_nomis_timestamp',
                          TO_CHAR(:NEW.create_datetime, 'YYYYMMDDHH24MISS.FF9'))
       )
      );
    END IF; --  if is_hmps_booking
  ELSIF UPDATING
    THEN
      IF    Is_Vch_Changed (:NEW.oic_sanction_code, :OLD.oic_sanction_code)
            OR Is_Int_Changed (:NEW.sanction_months, :OLD.sanction_months)
            OR Is_Int_Changed (:NEW.sanction_days, :OLD.sanction_days)
            OR Is_Vch_Changed (:NEW.status, :OLD.status)
      THEN
        IF Xtag_Trigger_Utils.is_hmps_booking(:NEW.offender_book_id)
        THEN
          xtag.Xtag_Notify
          (v_event_type      => 'A3_RESULT',
           v_params          => xtag.xtag_notification_params
           (xtag.xtag_params ('p_offender_book_id',
                              :NEW.offender_book_id
            ),
            xtag.xtag_params ('p_sanction_seq',
                              :NEW.sanction_seq
            ),
            xtag.xtag_params ('p_nomis_timestamp',
                              TO_CHAR(:NEW.modify_datetime, 'YYYYMMDDHH24MISS.FF9'))
           )
          );
        END IF; --  if is_hmps_booking
      END IF;
  END IF;
END;
/
ALTER TRIGGER "OFF_OIC_SANC_XTAG_EVENTS" ENABLE;

